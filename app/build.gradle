import groovy.json.JsonSlurper

import java.util.regex.Matcher
import java.util.regex.Pattern

/**
 * Copyright (c) 2023 Mindstix Software Labs
 * All rights reserved.
 */
plugins {
    // Android application plugin
    id 'com.android.application'
    // Kotlin Android plugin
    id 'org.jetbrains.kotlin.android'
    // Kotlin Annotation Processing Tool plugin
    id 'kotlin-kapt'
    // Dagger Hilt Android plugin
    id 'com.google.dagger.hilt.android'
}

// Apply custom configurations from app-flavours.gradle file
apply from: 'app-flavours.gradle'

android {
    // Set the namespace for the application
    namespace 'com.mindstix.baseline'
    // Set the compile SDK version
    compileSdk BuildConfig.compileSdk

    defaultConfig {
        // Application ID
        applicationId "com.mindstix.baseline"
        // Minimum SDK version
        minSdk BuildConfig.minSdk
        // Target SDK version
        targetSdk BuildConfig.targetSdk
        // Version code and version name
        versionCode BuildConfig.versionCode
        versionName BuildConfig.versionName

        // Test instrumentation runner for unit tests
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // Enable vector drawables using the support library
        vectorDrawables {
            useSupportLibrary true
        }

        def currentBuildFlavour = getCurrentFlavor()
        if (currentBuildFlavour.isEmpty()) {
            currentBuildFlavour = "indiadev"
            // set default value to avoid compilation issue of UnresolvedReference HEAP_ENV_ID, etc.
        }
        print("Current Floavour " + currentBuildFlavour)
        if (currentBuildFlavour.contains("india")) {
            if (currentBuildFlavour.contains("uat")) {
                getProps("./src/configuration/market/india/uat.props").each { props ->
                    buildConfigField "String", props.key, props.value
                }
            } else if (currentBuildFlavour.contains("qa")) {
                getProps("./src/configuration/market/india/qa.props").each { props ->
                    buildConfigField "String", props.key, props.value
                }
            } else if (currentBuildFlavour.contains("prod")) {
                getProps("./src/configuration/market/india/prod.props").each { props ->
                    buildConfigField "String", props.key, props.value
                }
            } else if (currentBuildFlavour.contains("dev")) {
                getProps("./src/configuration/market/india/dev.props").each { props ->
                    buildConfigField 'String', props.key, props.value
                }
            }
        }
    }

    buildTypes {
        release {
            // Disable minification for release builds
            minifyEnabled false
            // Specify ProGuard files
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        // Set Java source and target compatibility to version 17
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        // Set JVM target version
        jvmTarget = BuildConfig.jvmTarget
    }

    buildFeatures {
        // Enable Compose support
        compose true
        //Enable Build Configs
        buildConfig true
    }

    composeOptions {
        // Set the Kotlin compiler extension version for Compose
        kotlinCompilerExtensionVersion BuildConfig.kotlinCompilerExtensionVersion
    }

    packagingOptions {
        resources {
            // Exclude specific resources from packaging
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Core, Capabilities, Feature Modules Integration in the main app
    implementation project(':core')
    implementation project(':capabilities')
    implementation project(':features-onboarding')
    implementation project(':features-home')

    // Core dependencies
    implementation Deps.kore_ktx
    implementation Deps.lifecycle_runtime
    implementation Deps.activity_compose

    // Compose dependencies
    implementation platform(Deps.compose_bom)
    implementation Deps.compose_ui
    implementation Deps.compose_ui_graphics
    implementation Deps.ui_tooling_preview
    implementation Deps.material3
    implementation Deps.navigation_runtime_ktx

    // Testing dependencies
    // JUnit
    testImplementation Deps.junit
    // Android JUnit
    androidTestImplementation Deps.test_ext_junit
    // Espresso
    androidTestImplementation Deps.espresso
    // Compose testing
    androidTestImplementation platform(Deps.test_compose_bom)
    androidTestImplementation Deps.ui_test_junit4
    debugImplementation Deps.ui_tooling
    debugImplementation Deps.ui_test_manifest

    // Mockk for unit tests
    testImplementation Deps.mockk

    // Retrofit and networking dependencies
    implementation Deps.retrofit
    implementation Deps.retrofit_gson
    implementation Deps.okhttp

    // Dagger Hilt
    implementation Deps.hilt_android
    implementation Deps.hilt_navigation_compose
    kapt Deps.hilt_android_compiler
    kapt Deps.hilt_compiler
    kapt Deps.hilt_compiler_android

    // Splash Screen
    implementation Deps.splash_screen


    implementation("io.coil-kt:coil-compose:2.4.0")
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.8.6")
    implementation Deps.activity_compose    // Required for rememberLauncherForActivityResult

}

def getCurrentFlavor() {
    Gradle gradle = getGradle()
    String tskReqStr = gradle.getStartParameter().getTaskRequests().toString()

    Pattern pattern

    if (tskReqStr.contains("assemble")) // to run ./gradlew assembleRelease to build APK
        pattern = Pattern.compile("assemble(\\w+)(Release|Debug)")
    else if (tskReqStr.contains("bundle")) // to run ./gradlew bundleRelease to build .aab
        pattern = Pattern.compile("bundle(\\w+)(Release|Debug)")
    else if (tskReqStr.contains("test")) // to run ./gradlew testDebug to test
        pattern = Pattern.compile("test(\\w+)(Release|Debug)")
    else if (tskReqStr.contains("sonarqube"))
        pattern = Pattern.compile("sonarqube")
    else
        pattern = Pattern.compile("generate(\\w+)(Release|Debug)")

    Matcher matcher = pattern.matcher(tskReqStr)

    if (matcher.find() && matcher.groupCount() >= 2)
        return matcher.group(1).toLowerCase()
    else {
        return ""
    }
}

def getProps(path) {
    Properties props = new Properties()
    props.load(new FileInputStream(file(path)))
    return props
}

